#!/usr/bin/perl
use strict;

# Run one or more test files.

my $usage = "usage: run [-l less.exe] [-o lesstest-opts] [file.lt | dir]...\n";

use Getopt::Std;

my $testdir   = ".";
my $lesstest  = "$testdir/lesstest";
my $less      = "$testdir/../obj/less";
my $lesstest_opts = "";
my %opt;

exit (main() ? 0 : 1);
sub main {
	die $usage if not getopts('l:o:v', \%opt);
	die $usage if not @ARGV;
	$less = $opt{l} if $opt{l};
	$lesstest_opts = $opt{o};
	$lesstest_opts =~ s/^\s*//;
	$lesstest_opts =~ s/\s*$//;
	$lesstest_opts = '-'.$lesstest_opts if $lesstest_opts and $lesstest_opts !~ /^-/;

	my $errs = 0;
	foreach my $file (@ARGV) {
		$errs += run($file);
	}
	if ($errs > 0) {
		print STDERR "ERRS $errs errors\n";
		return 0;
	}
	return 1;
}

sub run {
	my ($file) = @_;
	if (-d $file) {
		return run_dir($file);
	}
	if ($file !~ /\.lt$/) {
		print "$0: WARNING skipping $file: not .lt file\n";
		return 0;
	}
	if (not -f $file) {
		print STDERR "ERR  cannot open $file\n";
		return 1;
	}
	##print STDERR "FILE $file\n";
	my $cmd = "$lesstest -s $testdir/lt_screen -t $file $lesstest_opts $less";
	print STDERR "RUN  $cmd\n" if $opt{v};
	if (system $cmd) {
		print STDERR "ERR  running $cmd\n";
		return 1;
	}
	return 0;
}

sub run_dir {
	my ($dir) = @_;
	my $errs = 0;
	my $dd;
	if (not opendir($dd, $dir)) {
		print STDERR "ERR  cannot open directory $dir\n";
		return 1;
	}
	while (my $entry = readdir($dd)) {
		next if $entry =~ /^\./;
		$errs += run("$dir/$entry");
	}
	closedir $dd;
	return $errs;
}
